import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlinVersion = '1.5.21'
    ext.kotlinCoroutinesVersion = '1.5.0'
    ext.ktorVersion = '1.6.2'
    ext.okhttpVersion = '4.9.1'
}

plugins {
    id("org.jetbrains.kotlin.jvm") version "$kotlinVersion"
    id('java-library')
    id("org.jetbrains.dokka") version "1.4.32"
    id("com.diffplug.spotless") version "5.15.0"
    id('jacoco')
    id('idea')

    id('signing')
    id('maven-publish')
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

apply from: 'jacoco.gradle'
apply from: 'release.gradle'

group = "tech.relaycorp"

repositories {
    mavenCentral()

  // TODO: Remove once all of Dokka's dependencies are available on Maven Central
  jcenter()
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom"))

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")

    api('tech.relaycorp:awala:[1.48.5,2.0.0)')
    testImplementation('tech.relaycorp:awala-testing:1.2.4')

    // We're depending on BouncyCastle just so we can use a base64 encoder on Android 21+. See:
    // https://github.com/relaycorp/awala-poweb-jvm/issues/66
    implementation("org.bouncycastle:bcpkix-jdk15on:1.69")

    // Handshake nonce signatures
    implementation("org.bouncycastle:bcpkix-jdk15on:1.69")

    // HTTP + WebSockets handling
    implementation("io.ktor:ktor-client-okhttp:$ktorVersion")
    testImplementation("io.ktor:ktor-client-mock:$ktorVersion")
    testImplementation("io.ktor:ktor-client-mock-jvm:$ktorVersion")
    testImplementation("com.squareup.okhttp3:okhttp:$okhttpVersion")
    testImplementation("com.squareup.okhttp3:mockwebserver:$okhttpVersion")
    testImplementation("com.squareup.okio:okio:2.10.0")
    testImplementation("org.awaitility:awaitility:4.1.0")

    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.junit.jupiter:junit-jupiter:5.7.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.2")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
    testImplementation("org.mockito:mockito-inline:3.11.2")
}

java {
    withJavadocJar()
    withSourcesJar()
}

kotlin {
    explicitApi = 'strict'
}

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += [
            "-Xopt-in=kotlin.RequiresOptIn"
        ]
    }
}

dokkaHtml.configure {
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)
            includes.from("api-docs.md")
        }
    }
}

spotless {
    def ktlintUserData = [
            "max_line_length": "100",
            "disabled_rules" : "import-ordering"
    ]
    kotlin {
        ktlint("0.36.0").userData(ktlintUserData)
    }
    kotlinGradle {
        ktlint().userData(ktlintUserData)
    }
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}
